import "../SensorReader.thingml"
import "datatypes.thingml" from stl

enumeration SensorType
@c_type	"uint8_t"
@c_byte_size "1"
{
	DIGITAL	@enum_val "0"
	ANALOG	@enum_val "1"
}

thing Sensor includes SensorFragment, TimerFragment
// See http://wiki.seeedstudio.com/Grove-Temperature_and_Humidity_Sensor_Pro/
@c_header `#include "DHT.h"`
@c_header `DHT dht(A2, DHT22);`
@c_header `bool startedDht = false;`
{
	property pin: Int8
	property stype: SensorType // 0 = digital, 1 = analog
	property sclass: UInt8 // 0 = Light, 1 = Movement, 2 = Temperature, 3 = Humidity
	
	function readSensor() : UInt16
	do
		if (sclass == 2) do return `dht.readTemperature();` end
		else if (sclass == 3) do return `dht.readHumidity();` end
		else if (stype == SensorType:DIGITAL) do return `(float)digitalRead(`&pin&`)` end
		else do return `(float)analogRead(`& pin &`)` end
	end
	
	statechart SensorReader init Reading {
		on entry do
			// sclass 2 = TempHum, See http://wiki.seeedstudio.com/Grove-Temperature_and_Humidity_Sensor_Pro/
			if (sclass == 2 or sclass == 3) do `if(!startedDht) { dht.begin(); startedDht = true; }` end
			else do `pinMode(` & pin & `, INPUT);` end
		end
		
		state Reading {
			internal event timer_interface?second
			action sensor_interface!data(sclass, readSensor())
		}
	}
}

thing LED includes LEDFragment {
	statechart LEDChart init Off {
		on entry do
			`pinMode(A3, OUTPUT);`
		end
		
		state Off {
			on entry `analogWrite(A3, 200);`
			
			transition ->On
			event led_interface?lightOn
		}
		
		state On {
			on entry `analogWrite(A3, 0);`
			
			transition ->Off
			event led_interface?lightOff
		}
	}
}

thing Arduino includes Controller, TimerFragment
@pim "Controller"
@platform "arduino"
{
	statechart ControllerStates init Waiting {
		state Waiting {
			on entry led_interface!lightOff()
			
			internal event m: sensor_interface?data
			action serial_interface!sensorData(m.sclass as UInt8, m.value)
			
			internal event serial_interface?ledOff
			action led_interface!lightOff()
			
			internal event serial_interface?ledOn
			action led_interface!lightOn()
		}
	}
}

protocol Timer;

protocol Serial
	@serial_baudrate		"115200"
	@serializer				"binary"
	@reverse_endianness     "true"
;

configuration SensorReaderCfg 
@arduino_stdout "Serial"
{
	// Sensors
	instance movementSensor: Sensor
		set movementSensor.pin = 2
		set movementSensor.stype = SensorType:DIGITAL
		set movementSensor.sclass = 1 
	instance lightSensor: Sensor
		set lightSensor.pin = `A0`
		set lightSensor.stype = SensorType:ANALOG
		set lightSensor.sclass = 0
	instance temperatureSensor: Sensor
		set temperatureSensor.pin = `A2`
		set temperatureSensor.stype = SensorType:ANALOG
		set temperatureSensor.sclass = 2
	instance humiditySensor: Sensor
		set humiditySensor.pin = `A2`
		set humiditySensor.stype = SensorType:ANALOG
		set humiditySensor.sclass = 3
	
	instance LED: LED
	instance controller: Arduino
	
	connector movementSensor.timer_interface over Timer
	connector lightSensor.timer_interface over Timer
	connector temperatureSensor.timer_interface over Timer
	connector humiditySensor.timer_interface over Timer
	connector controller.timer_interface over Timer
	
	connector controller.sensor_interface => movementSensor.sensor_interface
	connector controller.sensor_interface => lightSensor.sensor_interface
	connector controller.sensor_interface => temperatureSensor.sensor_interface
	connector controller.sensor_interface => humiditySensor.sensor_interface
	
	connector controller.led_interface => LED.led_interface
	
	connector controller.serial_interface over Serial
}